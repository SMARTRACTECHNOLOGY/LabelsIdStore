buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.akhikhl.gretty:gretty:+'
        classpath 'com.h2database:h2:1.4.191'
    }
}

plugins {
    id "org.flywaydb.flyway" version "4.1.2"
}

apply plugin: 'java'
apply plugin: 'scala'
apply plugin: 'eclipse'
apply plugin: 'war'
apply plugin: 'org.akhikhl.gretty'


def scalaMajor='2.11'
def scalaVersion='2.11.8'
def scalatraVersion='2.4.1'

configurations {
    provided
    grettyRunnerTomcat8 {
    	exclude group: 'org.slf4j', module: 'log4j-over-slf4j'
  	}
}
 
sourceSets {
    main.compileClasspath += configurations.provided
    test.compileClasspath += configurations.provided
    test.runtimeClasspath += configurations.provided

}
eclipse.classpath.plusConfigurations += [configurations.provided]


def env_ = System.getProperty("env", "local")

def dbUrl = System.properties["${env_}.blinkmob.dbUrl"]
def dbUser =  System.properties["${env_}.blinkmob.dbUser"]
def dbPassword = System.properties["${env_}.blinkmob.dbPassword"]


repositories {
    mavenCentral()
}

dependencies {

    compile "org.scala-lang:scala-library:$scalaVersion"
    compile "org.scala-lang:scala-compiler:$scalaVersion"
    compile "org.scalatra:scalatra_$scalaMajor:$scalatraVersion"
    compile "org.scalatra:scalatra-json_$scalaMajor:$scalatraVersion"
    compile "org.scalatra:scalatra-auth_$scalaMajor:$scalatraVersion"
    compile "org.json4s:json4s-jackson_$scalaMajor:3.3.0"
    compile "org.blinkmob:scala-tx-provider_$scalaMajor:1.0.1"
    compile "com.typesafe:config:1.3.1"
    
    compile group: 'com.zaxxer', name: 'HikariCP-java7', version: '2.4.11'
    compile "mysql:mysql-connector-java:5.1.35"
    compile group: 'com.typesafe.play', name: "anorm_$scalaMajor", version: '2.5.3'
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'
    
    
    
    testCompile "junit:junit:4.11"
    testCompile "org.specs2:specs2-junit_$scalaMajor:3.6.6"
    testCompile "org.scalatra:scalatra-specs2_$scalaMajor:$scalatraVersion"
    testCompile "org.specs2:specs2-core_2.11:3.8.5"
    testCompile "org.specs2:specs2-junit_2.11:3.8.5"
    provided 'javax.servlet:javax.servlet-api:3.1.0'
}

flyway {
    url = dbUrl
    user = dbUser
    password = dbPassword
    schemas = ['PUBLIC']
}

gretty{
	servletContainer 'tomcat8'
}

import org.akhikhl.gretty.AppStartTask
task('mAppRun', type: AppStartTask, dependsOn:'prepareInplaceWebApp') {
	doFirst {
        systemProperties = System.getProperties()
    }
}
mAppRun.dependsOn flywayMigrate



test {
	dependsOn "flywayMigrate"
    doFirst {
        systemProperties = System.getProperties()
    }
    testLogging.showStandardStreams = true
}

task repl(type:JavaExec) {
	systemProperties System.properties
    main = "scala.tools.nsc.MainGenericRunner"
    classpath = sourceSets.main.runtimeClasspath + sourceSets.test.runtimeClasspath
    standardInput System.in
    args '-usejavacp'
}

